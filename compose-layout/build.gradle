/*
 * Copyright 2022 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
    id "org.jetbrains.kotlin.kapt"
}

android {
    compileSdkVersion 31

    defaultConfig {
        minSdk 25
        targetSdk 30
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        buildConfig false
        compose true
    }

    kotlinOptions {
        jvmTarget = "1.8"
        // Allow for widescale experimental APIs in Alpha libraries we build upon
        freeCompilerArgs += "-opt-in=androidx.wear.compose.material.ExperimentalWearMaterialApi"
        freeCompilerArgs += "-opt-in=androidx.compose.foundation.ExperimentalFoundationApi"
        freeCompilerArgs += "-opt-in=androidx.compose.ui.ExperimentalComposeUiApi"
        freeCompilerArgs += "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
        freeCompilerArgs += "-opt-in=com.google.accompanist.pager.ExperimentalPagerApi"
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.compose.get()
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        // We run a full lint analysis as build part in CI, so skip vital checks for assemble tasks
        checkReleaseBuilds false
    }

    packagingOptions {
        // Some of the META-INF files conflict with coroutines-test. Exclude them to enable
        // our test APK to build (has no effect on our AARs)
        excludes += "/META-INF/AL2.0"
        excludes += "/META-INF/LGPL2.1"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        animationsDisabled true
    }

    sourceSets {
        test {
            java.srcDirs += 'src/sharedTest/kotlin'
            res.srcDirs += 'src/sharedTest/res'
        }
        androidTest {
            java.srcDirs += 'src/sharedTest/kotlin'
            res.srcDirs += 'src/sharedTest/res'
        }
    }
}

kapt {
    correctErrorTypes true
}

dependencies {
    implementation libs.kotlin.stdlib

    api libs.wearcompose.material
    api libs.wearcompose.foundation
    api libs.wearcompose.navigation

    api libs.accompanist.pager

    implementation libs.compose.ui.tooling
    implementation libs.compose.ui.util
    implementation libs.androidx.wear
    implementation libs.androidx.lifecycle.viewmodel.compose
    debugImplementation libs.compose.ui.toolingpreview
    debugImplementation libs.androidx.activity.compose

    implementation libs.androidx.navigation.compose
    implementation libs.androidx.navigation.ui.ktx

    androidTestImplementation libs.compose.ui.test.junit4
    debugImplementation libs.compose.ui.test.manifest
    androidTestImplementation libs.espresso.core
    androidTestImplementation libs.truth
    androidTestImplementation libs.junit
}

apply plugin: "com.vanniktech.maven.publish"
