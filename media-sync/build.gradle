/*
 * Copyright 2022 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
    id "org.jetbrains.kotlin.kapt"
}

android {
    compileSdkVersion 32

    defaultConfig {
        minSdk 26
        targetSdk 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        buildConfig false
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        resources {
            excludes += [
                '/META-INF/AL2.0',
                '/META-INF/LGPL2.1'
            ]
        }
    }


    sourceSets {
        main {
            assets.srcDirs = ['src/main/assets']
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        animationsDisabled true
    }
    lint {
        checkReleaseBuilds false
        textOutput file('stdout')
        textReport true
    }
    namespace 'com.google.android.horologist.media.sync'
}

project.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile.class).configureEach { task ->
    // Workaround for https://youtrack.jetbrains.com/issue/KT-37652
    if (!task.name.endsWith("TestKotlin") && !task.name.startsWith("compileDebug")) {
        task.kotlinOptions.freeCompilerArgs.add("-Xexplicit-api=strict")
    }
}

apply plugin: 'me.tylerbwong.gradle.metalava'

metalava {
    sourcePaths = ["src/main"]
    filename = "api/current.api"
    reportLintsAsErrors = true
}

dependencies {
    implementation libs.androidx.corektx
    implementation libs.androidx.startup
    implementation libs.androidx.tracing.ktx
    implementation libs.androidx.work.ktx

    implementation libs.hilt.ext.work
    implementation libs.dagger.hiltandroid
    kapt libs.dagger.hiltandroidcompiler
    kapt libs.hilt.ext.compiler

    androidTestImplementation libs.junit
    androidTestImplementation libs.truth
    androidTestImplementation libs.androidx.work.testing
    androidTestImplementation libs.dagger.hiltandroidtesting

    kaptAndroidTest libs.dagger.hiltandroidcompiler
    kaptAndroidTest libs.hilt.ext.compiler
}