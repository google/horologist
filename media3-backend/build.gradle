/*
 * Copyright 2022 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
    id "org.jetbrains.kotlin.kapt"
}

android {
    compileSdkVersion 32

    defaultConfig {
        minSdk 25
        targetSdk 30
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        buildConfig false
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
        freeCompilerArgs += "-opt-in=com.google.android.horologist.audio.ExperimentalHorologistAudioApi"
        freeCompilerArgs += "-opt-in=com.google.android.horologist.media.ExperimentalHorologistMediaApi"
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        // We run a full lint analysis as build part in CI, so skip vital checks for assemble tasks
        checkReleaseBuilds false
    }

    packagingOptions {
        // Some of the META-INF files conflict with coroutines-test. Exclude them to enable
        // our test APK to build (has no effect on our AARs)
        excludes += "/META-INF/AL2.0"
        excludes += "/META-INF/LGPL2.1"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        animationsDisabled true
    }

    sourceSets {
        test {
            java.srcDirs += 'src/sharedTest/kotlin'
            res.srcDirs += 'src/sharedTest/res'
        }
        androidTest {
            java.srcDirs += 'src/sharedTest/kotlin'
            res.srcDirs += 'src/sharedTest/res'
        }
    }
}

kapt {
    correctErrorTypes true
}

project.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile.class).configureEach { task ->
    // Workaround for https://youtrack.jetbrains.com/issue/KT-37652
    if (!task.name.endsWith("TestKotlin") && !task.name.startsWith("compileDebug")) {
        task.kotlinOptions.freeCompilerArgs.add("-Xexplicit-api=strict")
    }
}

dependencies {
    implementation projects.audio
    implementation projects.media
    implementation libs.kotlinx.coroutines.core
    api projectOrDependency(":media-lib-common", libs.androidx.media3.common)
    api libs.androidx.annotation
    api projectOrDependency(":media-lib-exoplayer", libs.androidx.media3.exoplayer)
    api projectOrDependency(":media-lib-exoplayer-dash", libs.androidx.media3.exoplayerdash)
    api projectOrDependency(":media-lib-exoplayer-hls", libs.androidx.media3.exoplayerhls)
    api projectOrDependency(":media-lib-exoplayer-rtsp", libs.androidx.media3.exoplayerrtsp)
    api projectOrDependency(":media-lib-session", libs.androidx.media3.session)
    implementation libs.androidx.lifecycle.process
    implementation libs.kotlinx.coroutines.guava
    implementation libs.androidx.corektx
    implementation libs.androidx.lifecycle.service

    testImplementation libs.junit
    testImplementation libs.truth
    testImplementation libs.androidx.test.ext.ktx
    testImplementation libs.kotlinx.coroutines.test
    testImplementation libs.robolectric
    testImplementation projectOrDependency(":media-test-utils", libs.androidx.media3.testutils)
    testImplementation projectOrDependency(":media-test-utils-robolectric", libs.androidx.media3.testutils.robolectric)
}

apply plugin: "com.vanniktech.maven.publish"
