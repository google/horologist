/*
 * Copyright 2022 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: "com.google.protobuf"
apply plugin: "com.google.android.libraries.mapsplatform.secrets-gradle-plugin"

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.google.android.horologist.sample"
        // Min because of Tiles
        minSdk 26
        targetSdk 30

        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders.schemeSuffix = "-debug"
        }
        release {
            manifestPlaceholders.schemeSuffix = ""

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig = signingConfigs.getByName("debug")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        compose true
    }

    kotlinOptions {
        jvmTarget = "1.8"
        // Allow for widescale experimental APIs in Alpha libraries we build upon
        freeCompilerArgs += "-opt-in=androidx.wear.compose.material.ExperimentalWearMaterialApi"
        freeCompilerArgs += "-opt-in=com.google.android.horologist.compose.navscaffold.ExperimentalHorologistComposeLayoutApi"
        freeCompilerArgs += "-opt-in=com.google.android.horologist.audio.ExperimentalHorologistAudioApi"
        freeCompilerArgs += "-opt-in=com.google.android.horologist.audio.ui.ExperimentalHorologistAudioUiApi"
        freeCompilerArgs += "-opt-in=com.google.android.horologist.auth.data.ExperimentalHorologistAuthDataApi"
        freeCompilerArgs += "-opt-in=com.google.android.horologist.auth.ui.ExperimentalHorologistAuthUiApi"
        freeCompilerArgs += "-opt-in=com.google.android.horologist.media.ui.ExperimentalHorologistMediaUiApi"
        freeCompilerArgs += "-opt-in=com.google.android.horologist.media.ExperimentalHorologistMediaApi"
        freeCompilerArgs += "-opt-in=androidx.compose.ui.ExperimentalComposeUiApi"
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
        freeCompilerArgs += "-opt-in=com.google.accompanist.pager.ExperimentalPagerApi"
        freeCompilerArgs += "-opt-in=com.google.android.horologist.composables.ExperimentalHorologistComposablesApi"
        freeCompilerArgs += "-opt-in=com.google.android.horologist.compose.tools.ExperimentalHorologistComposeToolsApi"
        freeCompilerArgs += "-opt-in=com.google.android.horologist.networks.ExperimentalHorologistNetworksApi"
        freeCompilerArgs += "-opt-in=com.google.android.horologist.tiles.ExperimentalHorologistTilesApi"
        freeCompilerArgs += "-opt-in=com.google.android.horologist.data.ExperimentalHorologistDataLayerApi"
        freeCompilerArgs += "-opt-in=androidx.lifecycle.compose.ExperimentalLifecycleComposeApi"
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.compose.compiler.get()
    }
    namespace 'com.google.android.horologist.sample'
}

sourceSets {
    main.java.srcDirs("build/generated/source/proto/debug/java")
    main.java.srcDirs("build/generated/source/proto/debug/grpc")
    main.java.srcDirs("build/generated/source/proto/debug/kotlin")
    main.java.srcDirs("build/generated/source/proto/debug/grpckt")
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.21.4'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
                kotlin {
                    option "lite"
                }
            }
        }
    }
}

dependencies {
    implementation projects.baseUi
    implementation projects.composeLayout
    implementation projects.audio
    implementation projects.audioUi
    implementation projects.authData
    implementation projects.authUi
    implementation projects.tiles
    implementation projects.composables
    implementation projects.media
    implementation projects.mediaUi
    implementation projects.networkAwareness
    implementation projects.tiles
    implementation projects.composeTools
    implementation projects.datalayer

    implementation libs.compose.ui.tooling
    implementation libs.compose.ui.util

    implementation libs.compose.foundation.foundation
    implementation libs.compose.material.iconsext

    implementation libs.wearcompose.material
    implementation libs.wearcompose.foundation
    implementation libs.wearcompose.navigation

    implementation libs.androidx.corektx
    implementation libs.androidx.fragment
    implementation libs.androidx.lifecycle.runtime
    implementation libs.androidx.lifecycle.viewmodel.compose
    implementation libs.androidx.activity.compose
    implementation libs.androidx.wear.tiles
    implementation libs.androidx.wear.tiles.material

    implementation libs.kotlinx.coroutines.playservices
    implementation libs.kotlin.stdlib

    implementation libs.coil

    implementation libs.lottie.compose

    implementation libs.protobuf.kotlin.lite

    implementation libs.moshi.kotlin

    debugImplementation projects.composeTools

    androidTestImplementation libs.compose.ui.test.junit4
    androidTestImplementation libs.espresso.core
    androidTestImplementation libs.junit
    androidTestImplementation libs.androidx.test.ext
    androidTestImplementation libs.androidx.test.ext.ktx
}

secrets {
    propertiesFileName "secrets.properties"
    defaultPropertiesFileName = 'secrets.defaults.properties'
}