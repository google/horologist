/*
 * Copyright 2022 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id("com.android.library")
    id("kotlin-android")
    id("org.jetbrains.dokka")
    id("org.jetbrains.kotlin.kapt")
}

android {
    compileSdkVersion = 33

    defaultConfig {
        //        Tiles is API 26, but if we don't stop this here, then we can't run the sample app
        //        on older devices (25).  We also don't want this to bleed into other modules via
        //        compose-tools which is used just for testing.
        minSdk = 25
        targetSdk = 30
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    buildFeatures {
        buildConfig = false
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    composeOptions {
        kotlinCompilerExtensionVersion = libs.versions.compose.compiler.get()
    }

    packagingOptions {
        resources {
            excludes += [
                "/META-INF/AL2.0",
                "/META-INF/LGPL2.1"
            ]
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        animationsDisabled = true
    }

    sourceSets {
        test {
            java.srcDirs += "src/sharedTest/kotlin"
            res.srcDirs += "src/sharedTest/res"
        }
        androidTest {
            java.srcDirs += "src/sharedTest/kotlin"
            res.srcDirs += "src/sharedTest/res"
        }
    }
    lint {
        checkReleaseBuilds = false
        textReport = true
    }
    namespace = "com.google.android.horologist.tiles"
}

kapt {
    correctErrorTypes true
}

project.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile.class).configureEach { task ->
    // Workaround for https://youtrack.jetbrains.com/issue/KT-37652
    if (!task.name.endsWith("TestKotlin") && !task.name.startsWith("compileDebug")) {
        task.kotlinOptions.freeCompilerArgs.add("-Xexplicit-api=strict")
    }
}

dependencies {
    implementation(libs.kotlin.stdlib)

    implementation(libs.androidx.wear)
    implementation(libs.androidx.lifecycle.runtime)
    implementation(libs.androidx.concurrent.future)

    api(libs.androidx.wear.tiles)
    api(libs.androidx.wear.tiles.material)
    api(libs.androidx.lifecycle.service)

    implementation(libs.coil)
    implementation(libs.wearcompose.foundation)

    implementation(libs.androidx.complications.datasource.ktx)
    api(libs.wearcompose.material)

    testImplementation(libs.androidx.wear.tiles.testing)
    testImplementation(libs.espresso.core)
    testImplementation(libs.junit)
    testImplementation(libs.truth)
    testImplementation(libs.robolectric)
    testImplementation(libs.kotlinx.coroutines.test)
    testImplementation(libs.androidx.concurrent.future.ktx)

    androidTestImplementation(libs.androidx.lifecycle.testing)
    androidTestImplementation(libs.kotlinx.coroutines.test)
    androidTestImplementation(libs.truth)
    debugImplementation(libs.compose.ui.test.manifest)
    androidTestImplementation(libs.espresso.core)
    androidTestImplementation(libs.junit)
}

apply plugin: "com.vanniktech.maven.publish"
